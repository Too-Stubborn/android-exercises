if (IS_MODULE.toBoolean()) {
    apply from: rootProject.ext.from.library
} else {
    apply from: rootProject.ext.from.application
}
//apply from: rootProject.ext.from.maven
apply plugin: rootProject.ext.plugin.butterknife

android {
    defaultConfig {
        if (!IS_MODULE.toBoolean()) {
            applicationId "com.xuhj.base.router"
            versionCode 1
            versionName "1.0.0"
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }

    sourceSets {
        def sampleDir = 'sample'
        main {
            if (IS_MODULE.toBoolean()) {
                //集成开发模式下排除debug文件夹中的所有Java文件
                java {
                    exclude "${sampleDir}/**"
                }
            } else {
                manifest.srcFile "src/${sampleDir}/AndroidManifest.xml"
                java.srcDir("src/${sampleDir}/java")
                res.srcDir("src/${sampleDir}/res")
            }
        }
    }
}

dependencies {
    // ButterKnife
    implementation "com.jakewharton:butterknife:$rootProject.ext.dependencies.butterknifeVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.ext.dependencies.butterknifeVersion"

    // alibaba - ARouter
    implementation 'com.alibaba:arouter-api:1.3.1'
    annotationProcessor 'com.alibaba:arouter-compiler:1.1.4'
}