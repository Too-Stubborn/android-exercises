/**
 * ******************** 打包相关配置 ********************
 */

// 获取当前时间戳串
def time() { return new Date().format("yyyyMMddHHss", TimeZone.getTimeZone("GMT+8")) }

android {
    defaultConfig {
        // 默认打包官方渠道Offical
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "Dev"]
    }
    //签名工具
    signingConfigs {
        release {
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
            storeFile file(rootProject.getRootDir().getAbsolutePath() + '/keystores/templateReleaseKey.jks')
            storePassword STORE_PASSWORD
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.release
        }
        release {
            //签名
            signingConfig signingConfigs.release
        }
    }
    // 维度，AS3.0以上需要所有渠道必须指定一个维度
    flavorDimensions 'default'
    //多渠道打包
    productFlavors {
        'Dev' {}
        'Testing' {}
        'Official' {}
        productFlavors.all { flavor ->
            flavor.dimension = 'default'
            flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
        }
    }
    //打包apk时自定义名字，as3.0需要修改为一下写法
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${variant.name}-v${variant.versionName}-${buildType.name}${time()}.apk"
        }
    }
    //apk前缀名字，此属性需要具体模块覆写app名称
    project.archivesBaseName = "AppName" + "-v" + defaultConfig.versionName
}